Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_PPMeter_DamageCombat_Statuses("PPMeter_DamageCombat_d1", 1);
DB_PPMeter_DamageCombat_Statuses("PPMeter_DamageCombat_d2", 2);
DB_PPMeter_DamageCombat_Statuses("PPMeter_DamageCombat_d3", 3);
DB_PPMeter_DamageCombat_Statuses("PPMeter_DamageCombat_d4", 4);
DB_PPMeter_DamageCombat_Statuses("PPMeter_DamageCombat_d5", 5);
DB_PPMeter_DamageCombat_Statuses("PPMeter_DamageCombat_d6", 6);
DB_PPMeter_DamageCombat_Statuses("PPMeter_DamageCombat_d7", 7);


DB_PPMeter_DigitStrToDuration("0", -600.0);
DB_PPMeter_DigitStrToDuration("1", -606.0);
DB_PPMeter_DigitStrToDuration("2", -612.0);
DB_PPMeter_DigitStrToDuration("3", -618.0);
DB_PPMeter_DigitStrToDuration("4", -624.0);
DB_PPMeter_DigitStrToDuration("5", -630.0);
DB_PPMeter_DigitStrToDuration("6", -636.0);
DB_PPMeter_DigitStrToDuration("7", -642.0);
DB_PPMeter_DigitStrToDuration("8", -648.0);
DB_PPMeter_DigitStrToDuration("9", -654.0);
KBSECTION
PROC
PROC_WriteNumberInUI((CHARACTER)_Character, (INTEGER)_Number)
AND
IntegerToString(_Number, _NumberStr)
THEN
PROC_ClearNumberInUI(_Character);
PROC_WriteOneByOne(_Character, _NumberStr);

PROC
PROC_ClearNumberInUI((CHARACTER)_Character)
AND
DB_PPMeter_DamageCombat_Statuses(_Status, _)
AND
HasAppliedStatus(_Character, _Status, 1)
THEN
RemoveStatus(_Character, _Status, _Character);

PROC
PROC_WriteOneByOne((CHARACTER)_Character, (STRING)_NumberStr)
AND
DB_PPMeter_DamageCombat_Statuses(_Status, _Start)
AND
Substring(_NumberStr, _Start, 1, _DigitStr)
AND
_DigitStr != ""
AND
DB_PPMeter_DigitStrToDuration(_DigitStr, _Duration)
THEN
ApplyStatus(_Character, _Status, _Duration, 1, _Character);


//REGION Add Show Off spell to each party member if it does not have it
IF
CastedSpell(_Character, _, _, _, _)
AND
DB_PartyMembers((CHARACTER)_Character)
AND
HasSpell(_Character, "PPMeterShowOff", 0)
THEN
AddSpell(_Character, "PPMeterShowOff", 1);
DebugText(_Character, "I can Show Off now!");
//END_REGION

//REGION ShowOff spell casted
IF
CastedSpell(_Character, "PPMeterShowOff", _, _, _)
AND
DB_PPMeter_DataInteger((CHARACTER)_Character, _Lifetime, "Damage", _Val1)
AND
DB_PPMeter_DataInteger((CHARACTER)_Character, _Lifetime, "RoundsCount", _Val2)
AND
Concatenate(_Lifetime, " Rounds: ", _msg1)
AND
ConcatenateInteger(_msg1, _Val2, _msg2)
AND
Concatenate(_msg2, " Damage: " , _msg3)
AND
ConcatenateInteger(_msg3, _Val1, _msg)
AND
IntegerToReal(_Val1, _Val1Real)
THEN
DebugText(_Character, _msg);



IF
CastedSpell(_Character, "PPMeterShowOff", _, _, _)
AND
NOT DB_PPMeter_DataInteger((CHARACTER)_Character, _, "Damage", _)
THEN
DebugText(_Character, "I need to hit someone first!");

IF
CastedSpell(_Character, "PPMeterShowOff", _, _, _)
AND
NOT DB_PPMeter_DataInteger((CHARACTER)_Character, _, "RoundsCount", _)
THEN
DebugText(_Character, "I need to end turn first!");
//END_REGION



IF
DB_PPMeter_DataInteger(_Character, _LifeTime, _StatsName, _Value)
AND
_LifeTime == "Combat"
AND
_StatsName == "Damage"
THEN
PROC_WriteNumberInUI(_Character, _Value);









EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start"
