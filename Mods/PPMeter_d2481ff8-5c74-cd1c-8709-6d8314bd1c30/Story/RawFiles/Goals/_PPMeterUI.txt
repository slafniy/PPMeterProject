Version 1
SubGoalCombiner SGC_AND
INITSECTION
// Mapping to Resource Name (names are declared in Mods/PPMeter_d2481ff8-5c74-cd1c-8709-6d8314bd1c30/Localization/English/PPMeterTechnicalStrings.loca.xml)
// ResourceName - StatsType - Lifetime
DB_PPMeter_ResourceNames("PPMeterCombatDamage", "Damage", "Combat");
DB_PPMeter_ResourceNames("PPMeterCombatRounds", "RoundsCount", "Combat");
DB_PPMeter_ResourceNames("PPMeterCombatDPR", "DPR", "Combat");
//DB_PPMeter_ResourceNames("PPMeter_DamageDay_d1", 1, "Damage", "Day");
//DB_PPMeter_ResourceNames("PPMeter_DamageLevel_d1", 1, "Damage", "Level");
//DB_PPMeter_ResourceNames("PPMeter_DamageOverall_d1", 1, "Damage", "Overall");
//DB_PPMeter_ResourceNames("PPMeter_RoundsDay_d1", 1, "RoundsCount", "Day");
//DB_PPMeter_ResourceNames("PPMeter_RoundsLevel_d1", 1, "RoundsCount", "Level");
//DB_PPMeter_ResourceNames("PPMeter_RoundsOverall_d1", 1, "RoundsCount", "Overall");
//DB_PPMeter_ResourceNames("PPMeter_DPRDay_d1", 1, "DPR", "Day");
//DB_PPMeter_ResourceNames("PPMeter_DPRLevel_d1", 1, "DPR", "Level");
//DB_PPMeter_ResourceNames("PPMeter_DPROverall_d1", 1, "DPR", "Overall");

// Save current resource boost to be able to remove it
// Character - ResourceName - BoostString
NOT DB_PPMeter_CurrentResourceBoost((CHARACTER)NULL_00000000-0000-0000-0000-000000000000, "", "");

KBSECTION
//REGION Write data to resource
// Clear old boost first!
PROC
PROC_PPMeter_WriteResource((CHARACTER)_Character, (STRING)_StatsType, (STRING)_Lifetime, (INTEGER)_Number)
AND
DB_PPMeter_ResourceNames(_ResourceName, _StatsType, _Lifetime)
AND
DB_PPMeter_CurrentResourceBoost(_Character, _ResourceName, _BoostString)
THEN
RemoveBoosts(_Character, _BoostString, 0, "", _Character);
NOT DB_PPMeter_CurrentResourceBoost(_Character, _ResourceName, _BoostString);

// Construct new boost string and apply it
PROC
PROC_PPMeter_WriteResource((CHARACTER)_Character, (STRING)_StatsType, (STRING)_Lifetime, (INTEGER)_Number)
AND
IntegerToString(_Number, _NumberStr)
AND
DB_PPMeter_ResourceNames(_ResourceName, _StatsType, _Lifetime)
AND
Concatenate("ActionResource(", _ResourceName, _str1)
AND
Concatenate(_str1, ",", _str2)
AND
Concatenate(_str2, _NumberStr, _str3)
AND
Concatenate(_str3, ",0)", _BoostStr)
THEN
DB_PPMeter_CurrentResourceBoost(_Character, _ResourceName, _BoostStr);
AddBoosts(_Character, _BoostStr, "", _Character);
//END_REGION


//REGION Update UI damage(set statuses) - looking if DB changed
IF
DB_PartyMembers(_Character)
AND
DB_PPMeter_DataInteger(_Character, _LifeTime, _StatsType, _Value)
THEN
PROC_PPMeter_WriteResource(_Character, _StatsType, _LifeTime, _Value);
//END_REGION

//REGION // PROC Force apply damage UI statuses
PROC
PROC_PPMeter_ForseUpdateStatuses((CHARACTER)_Character)
AND
DB_PPMeter_DataInteger(_Character, _LifeTime, _StatsType, _Value)
THEN
DebugText(_Character, "forced update");
PROC_PPMeter_WriteResource(_Character, _StatsType, _LifeTime, _Value);
//END_REGION


//REGION // DEBUG: on "oe pp" command - say all stats
IF
TextEvent("pp")
AND
DB_PartyMembers(_Character)
AND
DB_PPMeter_DataInteger(_Character, _Lifetime, "Damage", _Val1)
AND
DB_PPMeter_DataInteger(_Character, _Lifetime, "RoundsCount", _Val2)
AND
DB_PPMeter_DataInteger(_Character, _Lifetime, "DPR", _Val3)
AND
ConcatenateGUID("", _Character, _msg1)
AND
Concatenate(_msg1, _Lifetime, _msg2)
AND
Concatenate(_msg2, " Rounds: ", _msg3)
AND
ConcatenateInteger(_msg3, _Val2, _msg4)
AND
Concatenate(_msg4, " Damage: " , _msg5)
AND
ConcatenateInteger(_msg5, _Val1, _msg6)
AND
Concatenate(_msg6, " DPR: " , _msg7)
AND
ConcatenateInteger(_msg7, _Val3 , _msg)
THEN
DebugText(_Character, _msg);


IF
TextEvent("pp")
AND
DB_PartyMembers(_Character)
AND
NOT DB_PPMeter_DataInteger(_Character, _, "Damage", _)
THEN
DebugText(_Character, "I need to hit someone first!");

IF
TextEvent("pp")
AND
DB_PartyMembers(_Character)
AND
NOT DB_PPMeter_DataInteger(_Character, _, "RoundsCount", _)
THEN
DebugText(_Character, "I need to end turn first!");
//END_REGION


IF
TextEvent("dpr")
AND
DB_PartyMembers(_Character)
AND
DB_PPMeter_DataInteger(_Character, "Overall", "DPR", _DPR)
AND
ConcatenateInteger("Overall DPR: ", _DPR, _msg)
THEN
DebugText(_Character, _msg);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start"
